apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'

def webrtcVersion = System.getenv("WEBRTC_VERSION")
if (!webrtcVersion) {
    throw new GradleException("WEBRTC_VERSION environment variable is required but not set")
}
version = webrtcVersion

configurations {
    aarLocal
}

def aarFile = file('../out/libwebrtc.aar')
def aarArtifact = artifacts.add('aarLocal', aarFile) {
    type 'aar'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.genesys.purecloud.android.webrtc'
            artifactId = 'library'
            version = version
            artifact aarArtifact
        }
    }
}

task verifyVersion {
    doLast {
        println "WEBRTC_VERSION: ${version}"
        println "AAR file exists: ${aarFile.exists()}"
        println "Ready to publish version ${version}"
    }
}

artifactory {
    contextUrl = secureProperties["artifactory_contextUrl"]
    //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
        repository {
            repoKey = 'inin-android'
            username = secureProperties["artifactoryUsername"]
            password = secureProperties["artifactoryPassword"]
        }
        defaults {
            publications('maven')
            publishPom = true //Publish generated POM files to Artifactory (true by default)
        }
    }
}
